version '1.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'findbugs'

// Makes the command './gradlew run' run the given main class.
apply plugin: 'application'
mainClassName = 'client.ConsoleClient'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

// Modify run task such that it uses stdin for input from the user
// and also supply the main method with provided arguments.
run {
    standardInput = System.in
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}

// Ensure findBugs returns an html report
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    effort = "max"
    reportLevel = "low"
    ignoreFailures = true
}

// Adjusting this variable automatically changes the imported jersey libraries
ext.jerseyVersion = '2.26'

dependencies {
    // Import Jersey
    compile 'org.glassfish.jersey.core:jersey-client:'+jerseyVersion
    compile 'org.glassfish.jersey.core:jersey-common:'+jerseyVersion
    compile 'org.glassfish.jersey.inject:jersey-hk2:'+jerseyVersion

    // Gson for JSON parsing.
    compile 'com.google.code.gson:gson:2.4'

    // Client / Server Common dependencies
    compile files('common-1.0.jar')
}

